##
## General Doing patterns described here:
## 
Action is a class.

# set values as third-party
     On "ONE is not DOING anything.", think "destroy link ONE DOING!".
Then on "ONE is not DOING anything.", reply "ok".
        "ONE is not DOING anything." implies "is ONE a person?"; if not, reply "ONE is not a person".
        "ONE is not DOING anything." implies "is DOING an action?"; if not, reply "DOING is not an action".

     On "ONE is DOING TWO's OBJECT.", think "set link ONE DOING TWO OBJECT!".
Then on "ONE is DOING TWO's OBJECT.", reply "ok".
        "ONE is DOING TWO's OBJECT." implies "Is TWO a person?"; if not, reply "TWO is not a person".
        "ONE is DOING TWO's OBJECT." implies "Is ONE a person?"; if not, reply "ONE is not a person".
        "ONE is DOING TWO's OBJECT." implies "Is DOING an action?"; if not, reply "DOING is not an action".

     On "ONE is not DOING TWO's OBJECT.", think "destroy link ONE DOING TWO OBJECT!".
Then on "ONE is not DOING TWO's OBJECT.", if not, reply "error: link destroy ONE DOING TWO OBJECT".
Then on "ONE is not DOING TWO's OBJECT.", reply "ok".
        "ONE is not DOING TWO's OBJECT." implies "Is TWO a person?"; if not, reply "TWO is not a person".
        "ONE is not DOING TWO's OBJECT." implies "Is ONE a person?"; if not, reply "ONE is not a person".
        "ONE is not DOING TWO's OBJECT." implies "Is DOING an action?"; if not, reply "DOING is not an action".

# object/person-specific query state of affairs
     On "whose OBJECT is PERSON DOING?", think "get link PERSON DOING!".
Then on "whose OBJECT is PERSON DOING?", if not, reply "PERSON is not DOING anyone's OBJECT".
Then on "whose OBJECT is PERSON DOING?", reply "PERSON is DOING ...".
        "Whose OBJECT is PERSON DOING?" implies "Is PERSON a person?"; if not, reply "PERSON is not a person".
        "Whose OBJECT is PERSON DOING?" implies "Is DOING an action?"; if not, reply "DOING is not an action".

# general query - action specific
     On "What is X DOING?", think "get link X DOING!".
Then on "What is X DOING?", if not, reply "X is not DOING anything".
Then on "What is X DOING?", reply "X is DOING ...".

#
# person/object/action-specific query - is ruth holding martin's hand?
     On "is ONE DOING TWO's OBJECT?", think "link ONE DOING TWO OBJECT exists?".
Then on "is ONE DOING TWO's OBJECT?", if not, reply "no". 
Then on "is ONE DOING TWO's OBJECT?", reply "yes".
        "is ONE DOING TWO's OBJECT?" implies "Is TWO a person?"; if not, reply "TWO is not a person".
        "is ONE DOING TWO's OBJECT?" implies "Is ONE a person?"; if not, reply "ONE is not a person".
        "is ONE DOING TWO's OBJECT?" implies "Is DOING an action?"; if not, reply "DOING is not an action".
