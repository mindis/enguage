####################################################
### I need X
###
### The latest version of this file can be found at:
### http://www.yagadi.com/assets/need.txt (c) yagadi.com, 2013-5.
### 
####################################################

####################################################
# Third-person utterances
####################################################
On "SUBJECT needs PHRASE-OBJECT":
	OBJECT exists in SUBJECT needs list;
	reply "I know";
	if not, add OBJECT to SUBJECT needs list;
	then, reply "ok, SUBJECT needs OBJECT".
On "SUBJECT needs NUMERIC-QUANTITY PHRASE-OBJECT":
	perform "item set format 'QUANTITY,UNIT of,,from FROM'";
	QUANTITY OBJECT exists in SUBJECT needs list;
	reply "I know";
	if not, add QUANTITY OBJECT to SUBJECT needs list;
	then, reply "ok, SUBJECT needs ... ".
On "SUBJECT needs NUMERIC-QUANTITY UNIT of PHRASE-OBJECT":
	perform "item set format 'QUANTITY,UNIT of,,from FROM'";
	QUANTITY UNIT of OBJECT exists in SUBJECT needs list;
	reply "I know";
	if not, add QUANTITY UNIT of OBJECT to SUBJECT needs list;
	then, reply "ok, SUBJECT needs ...".


On "SUBJECT has PHRASE-OBJECT", SUBJECT does not need OBJECT.

On "SUBJECT has everything":
	delete SUBJECT needs list;
	then, reply "ok, SUBJECT does not need anything".

On "SUBJECT does not need PHRASE-OBJECT":
	remove OBJECT from SUBJECT needs list;
	if not, reply "No, I do not know what you mean by OBJECT";
	then, reply "ok, SUBJECT does not need OBJECT".
On "SUBJECT does not need NUMERIC-QUANTITY PHRASE-OBJECT":
	remove QUANTITY OBJECT from SUBJECT needs list;
	if not, reply "No, SUBJECT does not need QUANTITY OBJECT";
	then, reply "ok, SUBJECT does not need QUANTITY OBJECT".
On "SUBJECT does not need NUMERIC-QUANTITY UNIT of PHRASE-OBJECT":
	remove QUANTITY UNIT of OBJECT from SUBJECT needs list;
	if not, reply "No, SUBJECT does not need QUANTITY UNIT of OBJECT";
	then, reply "ok, SUBJECT does not need QUANTITY UNIT of OBJECT".
On "SUBJECT does not need any PHRASE-OBJECT":
	remove any OBJECT from SUBJECT needs list; # "SUBJECT needs [any] OBJECT"
	if not, reply "No, SUBJECT does not need any OBJECT";
	then, reply "ok, SUBJECT does not need any OBJECT".


On "what does SUBJECT need":
	perform "item set format 'QUANTITY,UNIT of,,from FROM'";
	get SUBJECT needs list;
	if not, reply "SUBJECT does not need anything";
	then, reply "SUBJECT needs ...".

On "does SUBJECT need PHRASE-OBJECT":
	perform "list exists SUBJECT needs OBJECT";
	if not, reply "no, SUBJECT does not need OBJECT";
	then, reply "yes, SUBJECT needs OBJECT".
On "does SUBJECT need NUMERIC-QUANTITY PHRASE-OBJECT":
	perform "list exists SUBJECT needs OBJECT QUANTITY";
	if not, reply "no, SUBJECT does not need QUANTITY OBJECT";
	then, reply "yes, SUBJECT needs QUANTITY OBJECT".
On "does SUBJECT need NUMERIC-QUANTITY UNIT of PHRASE-OBJECT":
	perform "list exists SUBJECT needs OBJECT QUANTITY UNIT";
	if not, reply "no, SUBJECT does not need QUANTITY UNIT of OBJECT";
	then, reply "yes, SUBJECT needs QUANTITY UNIT of OBJECT".

On "how many UNIT of PHRASE-OBJECT does SUBJECT need":
	how many UNIT of OBJECT in SUBJECT needs list;
	then, reply "..., SUBJECT needs ... UNIT of OBJECT".
On "how many PHRASE-OBJECT does SUBJECT need":
	how many OBJECT in SUBJECT needs list;
	then, reply "..., SUBJECT needs ... OBJECT".

######################################
# First person utterances
######################################
On "I        need        PHRASE-OBJECT", _user needs OBJECT.
On "help", reply "to add items to the list".
On "get                  PHRASE-OBJECT", _user needs OBJECT.
On "get         SUBJECT        PHRASE-OBJECT", SUBJECT needs OBJECT.

On "I        have to have PHRASE-OBJECT", _user needs OBJECT.

On "I        have to     PHRASE-OBJECT", _user needs to OBJECT.
On "I do not have to     PHRASE-OBJECT", _user does not need to OBJECT.
On "I        need to     PHRASE-OBJECT", _user needs to OBJECT.
On "I        have to get PHRASE-OBJECT", _user needs OBJECT.
On "I        need to get PHRASE-OBJECT", _user needs OBJECT.
On "I do not have to get PHRASE-OBJECT", _user does not need OBJECT.
On "I do not need to get PHRASE-OBJECT", _user does not need OBJECT.

On "I do not need        PHRASE-OBJECT", _user does not need OBJECT.
On "I do not need any    PHRASE-OBJECT", _user does not need any OBJECT.
On "I        have got    PHRASE-OBJECT", _user does not need OBJECT.
On "I        have        PHRASE-OBJECT", _user does not need OBJECT.
On "         clear the       list", _user has everything.
On "I        have got  everything", _user has everything.
On "I        have      everything", _user has everything.
On "I do not need        anything", _user has everything.
On "help", reply "to clear the list".

On                   "?", what does _user need.
On "what  do   I   need", what does _user need.
On "help", reply "to find out what is on the list".

On "do I need PHRASE-OBJECT", does _user need OBJECT.
On "how many PHRASE-OBJECT do I need", how many OBJECT does _user need.
On "help", reply "to find out if something specific is on the list".

On "We need PHRASE-OBJECT", I need OBJECT. # a fix for now
